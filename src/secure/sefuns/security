/// @addtogroup sefun
/// @{
/// @file security
/// @brief security related simul_efuns
/// @author Gwenhwyvar
/// @version 0.1.0
/// @date 2016-01-24

// getgid {{{
// --------------------------------------------------------------------------
/// @brief getgid 
/// 
/// @Param who - defaults to this_object()
/// @Returns real group id
// --------------------------------------------------------------------------
public string getgid(mixed who = 0)
{
    if(!who)
        who = TO();

    return explode(efun::getuid(who), ":")[1];
}
// }}}
// getegid {{{
// --------------------------------------------------------------------------
/// @brief getegid 
/// 
/// @Param who - defaults to this_object()
/// @Returns effective group id
// --------------------------------------------------------------------------
public string getegid(mixed who = 0)
{
    string ret;

    if(!who)
        who = TO();

    ret = efun::geteuid(who);
    if(!ret)
        return UNKNOWN_DOMAIN;
    else
        return explode(ret, ":")[1];
}
// }}}
// setegid {{{
// --------------------------------------------------------------------------
/// @brief setegid 
/// 
/// @Param gid
/// @Returns 1 if successfully set, 0 otherwise
// --------------------------------------------------------------------------
public int setegid(string egid)
{
    object    who = TO();
    function    f = bind( (: efun::seteuid :), who);
    string    arg = geteuid(who) + ":" + egid;

    return (*f)(arg);
}
// }}}
// getuid {{{
// --------------------------------------------------------------------------
/// @brief getuid 
/// 
/// @Param who - defaults to this_object()
/// @Returns real user id
// --------------------------------------------------------------------------
public string getuid(mixed who = 0)
{
    if(!who)
        who = TO();

    return explode(efun::getuid(who), ":")[0];
}
// }}}
// geteuid {{{
// --------------------------------------------------------------------------
/// @brief geteuid 
/// 
/// @Param who - defaults to this_object()
/// @Returns effective user id
// --------------------------------------------------------------------------
public string geteuid(mixed who = 0)
{
    string ret;

    if(!who)
        who = TO();

    ret = efun::geteuid(who);
    if(!ret)
        return UNKNOWN_UID;
    else
        return explode(ret, ":")[0];
}
// }}}
// seteuid {{{
// --------------------------------------------------------------------------
/// @brief seteuid 
/// 
/// @Param euid
/// @Returns 1 if successfully set, 0 otherwise
// --------------------------------------------------------------------------
public int seteuid(string euid)
{
    object    who = TO();
    function    f = bind( (: efun::seteuid :), who);
    string    arg = euid + ":" + getegid(who);;

    return (*f)(arg);
}
// }}}
// init_eids {{{
// --------------------------------------------------------------------------
/// @brief init_eids 
///
/// This function must be called within each object that wants to do any
/// security relevant action (e.g. read/write files, clone other objects, ...)
/// @Returns 1 if successfully set, 0 otherwise
// --------------------------------------------------------------------------
public int init_eteids(void)
{
    object   who = TO();
    function   f = bind( (: efun::seteuid :), who);

    return (*f)(efun::getuid(who));
}
// }}}

// playerp {{{
// --------------------------------------------------------------------------
/// @brief playerp 
///
/// This function tests if the supplied object is a player
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int playerp(object ob)
{
    return ob == find_player(getuid(ob));
}
// }}}
// elderp {{{
// --------------------------------------------------------------------------
/// @brief elderp 
///
/// This function tests if the supplied object has elder rights
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int elder(object ob)
{
    return playerp(ob) && (member_array(UPRIV_ELDER, (string)MUD_INFO_D->get_groups(getuid(ob))) != -1);
}
// }}}
// creatorp {{{
// --------------------------------------------------------------------------
/// @brief creatorp 
///
/// This function tests if the supplied object has creator rights (is wizard)
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int creatorp(object ob)
{
    return playerp(ob) && (member_array(UPRIV_WIZARD, (string)MUD_INFO_D->get_groups(getuid(ob))) != -1);
}
// }}}
// Dcreatorp {{{
// --------------------------------------------------------------------------
/// @brief Dcreatorp 
///
/// This function tests if the supplied object has creator rights (is wizard)
/// and is member of either the supplied or any domain
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int Dcreatorp(object ob, string domain = "@any@")
{
    string uid;

    // only creators may be domain member
    if(!creatorp(ob))
        return FALSE;

    uid = getuid(ob);
    if(domain == "@any@")
    {
        // check every domain
        foreach(domain in (string)MUD_INFO_D->get_domains())
        {
            if(member_array(uid, (string)MUD_INFO_D->get_domain_member(domain)) != -1)
                return TRUE;
        }
        // not found...
        return FALSE;
    }
    return member_array(uid, (string)MUD_INFO_D->get_domain_member(domain)) != -1;
}
// }}}
// Dlordp {{{
// --------------------------------------------------------------------------
/// @brief Dlordp 
///
/// This function tests if the supplied object has creator rights (is wizard)
/// and is domain lord of either the supplied or any domain
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int Dlordp(object ob, string domain = "@any@")
{
    string  uid;

    // only creators may be domain lords (well domain members but that
    // distinction doesn't matter down below)
    if(!creatorp(ob))
        return FALSE;

    uid = getuid(ob);
    if(domain == "@any@")
    {
        // check every domain
        foreach(domain in (string)MUD_INFO_D->get_domains())
        {
            if(member_array(uid, (string)MUD_INFO_D->get_domain_lords(domain)) != -1)
                return TRUE;
        }
        // not found...
        return FALSE;
    }

    return member_array(uid, (string)MUD_INFO_D->get_domain_lords(domain)) != -1;
}
// }}}
// archp {{{
// --------------------------------------------------------------------------
/// @brief archp 
///
/// This function tests if the supplied object has archwiz rights
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int archp(object ob)
{
    return creatorp(ob) && (member_array(UPRIV_ARCH, (string)MUD_INFO_D->getgroups(geteuid(ob))) != -1);
}
// }}}
// adminp {{{
// --------------------------------------------------------------------------
/// @brief adminp 
///
/// This function tests if the supplied object has admin rights
/// @Returns TRUE/FALSE depending on result
// --------------------------------------------------------------------------
public int adminp(object ob)
{
    return creatorp(ob) && (member_array(UPRIV_ADMIN, (string)MUD_INFO_D->get_groups(getuid(ob))) != -1);
}
// }}}
///  @}
